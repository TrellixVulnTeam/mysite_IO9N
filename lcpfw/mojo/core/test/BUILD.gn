# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/protobuf/proto_library.gni")

static_library("test_support") {
  testonly = true
  sources = [
    "mock_node_channel_delegate.cc",
    "mock_node_channel_delegate.h",
    "mojo_test_base.cc",
    "mojo_test_base.h",
    "test_utils.h",
  ]

  if (!is_ios) {
    sources += [
      "multiprocess_test_helper.cc",
      "multiprocess_test_helper.h",
    ]
  }

  if (is_win) {
    sources += [ "test_utils_win.cc" ]
  }

  if (is_fuchsia || is_posix) {
    sources += [ "test_utils.cc" ]
  }

  public_deps = [
    "//base",
    "//base/test:test_support",
    "//mojo/core:embedder_internal",
    "//mojo/core/embedder",
    "//mojo/public/cpp/system",
    #"//testing/gmock",
    #"//testing/gtest",
  ]
}

if (is_mac) {
  proto_library("channel_mac_proto") {
    proto_in_dir = "//"
    sources = [ "data/channel_mac/channel_mac.proto" ]
    link_deps = [ "//testing/libfuzzer/fuzzers/mach:proto" ]
  }
}
