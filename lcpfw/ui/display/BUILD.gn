# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//build/config/ui.gni")
import("//testing/test.gni")
import("//ui/display/display.gni")

component("display") {
  sources = [
    "display.cc",
    "display.h",
    "display_change_notifier.cc",
    "display_change_notifier.h",
    "display_export.h",
    "display_features.cc",
    "display_features.h",
    "display_finder.cc",
    "display_finder.h",
    "display_layout.cc",
    "display_layout.h",
    "display_layout_builder.cc",
    "display_layout_builder.h",
    "display_list.cc",
    "display_list.h",
    "display_observer.cc",
    "display_observer.h",
    "display_switches.cc",
    "display_switches.h",
    "display_transform.cc",
    "display_transform.h",
    "scoped_display_for_new_windows.cc",
    "scoped_display_for_new_windows.h",
    "screen.cc",
    "screen.h",
    "screen_base.cc",
    "screen_base.h",
    "tablet_state.h",
    "unified_desktop_utils.cc",
    "unified_desktop_utils.h",
  ]

  if (is_android) {
    sources += [ "screen_android.cc" ]
  }

  if (is_mac) {
    sources += [
      "mac/display_link_mac.cc",
      "mac/display_link_mac.h",
      "mac/screen_mac.mm",
    ]
  }

  if (is_win) {
    sources += [
      "win/base_window_finder_win.cc",
      "win/base_window_finder_win.h",
      "win/color_profile_reader.cc",
      "win/color_profile_reader.h",
      "win/display_info.cc",
      "win/display_info.h",
      "win/dpi.cc",
      "win/dpi.h",
      "win/local_process_window_finder_win.cc",
      "win/local_process_window_finder_win.h",
      "win/scaling_util.cc",
      "win/scaling_util.h",
      "win/screen_win.cc",
      "win/screen_win.h",
      "win/screen_win_display.cc",
      "win/screen_win_display.h",
      "win/topmost_window_finder_win.cc",
      "win/topmost_window_finder_win.h",
      "win/uwp_text_scale_factor.cc",
      "win/uwp_text_scale_factor.h",
    ]
  }

  if (is_ios) {
    sources += [ "ios/screen_ios.mm" ]
  }

  defines = [ "DISPLAY_IMPLEMENTATION" ]

  public_deps = [
    "//ui/display/types",
    "//ui/gfx:color_space",
    "//ui/gfx:gfx",
  ]

  deps = [
    "//base",
    "//build:chromeos_buildflags",
    "//mojo/public/cpp/bindings:struct_traits",
    "//ui/display/util",
    "//ui/gfx",
    "//ui/gfx/geometry",
  ]

  if (is_win) {
    # Don't conflict with Windows' "display.dll".
    output_name = "ui_display"
  }

  if (use_aura) {
    sources += [ "screen_aura.cc" ]
  }

  if (is_mac) {
    frameworks = [
      "AppKit.framework",
      "CoreGraphics.framework",
      "QuartzCore.framework",
    ]
  }
}
