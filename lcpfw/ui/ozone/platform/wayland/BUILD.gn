# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

visibility = [ "//ui/ozone/*" ]

import("//build/config/chromeos/ui_mode.gni")
import("//build/config/linux/gtk/gtk.gni")
import("//build/config/linux/pkg_config.gni")
import("//gpu/vulkan/features.gni")
import("//testing/libfuzzer/fuzzer_test.gni")
import("//third_party/wayland/features.gni")
import("//ui/ozone/platform/wayland/wayland.gni")

source_set("wayland") {
  sources = [
    "client_native_pixmap_factory_wayland.cc",
    "client_native_pixmap_factory_wayland.h",
    "common/data_util.cc",
    "common/data_util.h",
    "common/wayland.h",
    "common/wayland_object.cc",
    "common/wayland_object.h",
    "common/wayland_util.cc",
    "common/wayland_util.h",
    "gpu/drm_render_node_path_finder.cc",
    "gpu/drm_render_node_path_finder.h",
    "gpu/gl_surface_egl_readback_wayland.cc",
    "gpu/gl_surface_egl_readback_wayland.h",
    "gpu/gl_surface_wayland.cc",
    "gpu/gl_surface_wayland.h",
    "gpu/wayland_buffer_manager_gpu.cc",
    "gpu/wayland_buffer_manager_gpu.h",
    "gpu/wayland_canvas_surface.cc",
    "gpu/wayland_canvas_surface.h",
    "gpu/wayland_overlay_candidates.cc",
    "gpu/wayland_overlay_candidates.h",
    "gpu/wayland_overlay_manager.cc",
    "gpu/wayland_overlay_manager.h",
    "gpu/wayland_surface_factory.cc",
    "gpu/wayland_surface_factory.h",
    "gpu/wayland_surface_gpu.h",
    "host/gtk_primary_selection_device.cc",
    "host/gtk_primary_selection_device.h",
    "host/gtk_primary_selection_device_manager.cc",
    "host/gtk_primary_selection_device_manager.h",
    "host/gtk_primary_selection_offer.cc",
    "host/gtk_primary_selection_offer.h",
    "host/shell_object_factory.cc",
    "host/shell_object_factory.h",
    "host/shell_popup_wrapper.cc",
    "host/shell_popup_wrapper.h",
    "host/shell_surface_wrapper.h",
    "host/shell_toplevel_wrapper.cc",
    "host/shell_toplevel_wrapper.h",
    "host/wayland_auxiliary_window.cc",
    "host/wayland_auxiliary_window.h",
    "host/wayland_buffer_manager_connector.cc",
    "host/wayland_buffer_manager_connector.h",
    "host/wayland_buffer_manager_host.cc",
    "host/wayland_buffer_manager_host.h",
    "host/wayland_clipboard.cc",
    "host/wayland_clipboard.h",
    "host/wayland_connection.cc",
    "host/wayland_connection.h",
    "host/wayland_cursor.cc",
    "host/wayland_cursor.h",
    "host/wayland_cursor_factory.cc",
    "host/wayland_cursor_factory.h",
    "host/wayland_cursor_position.cc",
    "host/wayland_cursor_position.h",
    "host/wayland_data_device.cc",
    "host/wayland_data_device.h",
    "host/wayland_data_device_base.cc",
    "host/wayland_data_device_base.h",
    "host/wayland_data_device_manager.cc",
    "host/wayland_data_device_manager.h",
    "host/wayland_data_drag_controller.cc",
    "host/wayland_data_drag_controller.h",
    "host/wayland_data_offer.cc",
    "host/wayland_data_offer.h",
    "host/wayland_data_offer_base.cc",
    "host/wayland_data_offer_base.h",
    "host/wayland_data_source.cc",
    "host/wayland_data_source.h",
    "host/wayland_drm.cc",
    "host/wayland_drm.h",
    "host/wayland_event_source.cc",
    "host/wayland_event_source.h",
    "host/wayland_event_watcher.cc",
    "host/wayland_event_watcher.h",
    "host/wayland_input_method_context.cc",
    "host/wayland_input_method_context.h",
    "host/wayland_input_method_context_factory.cc",
    "host/wayland_input_method_context_factory.h",
    "host/wayland_keyboard.cc",
    "host/wayland_keyboard.h",
    "host/wayland_menu_utils.cc",
    "host/wayland_menu_utils.h",
    "host/wayland_output.cc",
    "host/wayland_output.h",
    "host/wayland_output_manager.cc",
    "host/wayland_output_manager.h",
    "host/wayland_pointer.cc",
    "host/wayland_pointer.h",
    "host/wayland_popup.cc",
    "host/wayland_popup.h",
    "host/wayland_screen.cc",
    "host/wayland_screen.h",
    "host/wayland_shm.cc",
    "host/wayland_shm.h",
    "host/wayland_shm_buffer.cc",
    "host/wayland_shm_buffer.h",
    "host/wayland_subsurface.cc",
    "host/wayland_subsurface.h",
    "host/wayland_surface.cc",
    "host/wayland_surface.h",
    "host/wayland_toplevel_window.cc",
    "host/wayland_toplevel_window.h",
    "host/wayland_touch.cc",
    "host/wayland_touch.h",
    "host/wayland_window.cc",
    "host/wayland_window.h",
    "host/wayland_window_drag_controller.cc",
    "host/wayland_window_drag_controller.h",
    "host/wayland_window_factory.cc",
    "host/wayland_window_manager.cc",
    "host/wayland_window_manager.h",
    "host/wayland_window_observer.cc",
    "host/wayland_window_observer.h",
    "host/wayland_zaura_shell.cc",
    "host/wayland_zaura_shell.h",
    "host/wayland_zcr_cursor_shapes.cc",
    "host/wayland_zcr_cursor_shapes.h",
    "host/wayland_zwp_linux_dmabuf.cc",
    "host/wayland_zwp_linux_dmabuf.h",
    "host/xdg_foreign_wrapper.cc",
    "host/xdg_foreign_wrapper.h",
    "host/xdg_popup_wrapper_impl.cc",
    "host/xdg_popup_wrapper_impl.h",
    "host/xdg_surface_wrapper_impl.cc",
    "host/xdg_surface_wrapper_impl.h",
    "host/xdg_toplevel_wrapper_impl.cc",
    "host/xdg_toplevel_wrapper_impl.h",
    "host/zwp_primary_selection_device.cc",
    "host/zwp_primary_selection_device.h",
    "host/zwp_primary_selection_device_manager.cc",
    "host/zwp_primary_selection_device_manager.h",
    "host/zwp_primary_selection_offer.cc",
    "host/zwp_primary_selection_offer.h",
    "host/zwp_text_input_wrapper.h",
    "host/zwp_text_input_wrapper_v1.cc",
    "host/zwp_text_input_wrapper_v1.h",
    "host/zxdg_popup_v6_wrapper_impl.cc",
    "host/zxdg_popup_v6_wrapper_impl.h",
    "host/zxdg_surface_v6_wrapper_impl.cc",
    "host/zxdg_surface_v6_wrapper_impl.h",
    "host/zxdg_toplevel_v6_wrapper_impl.cc",
    "host/zxdg_toplevel_v6_wrapper_impl.h",
    "ozone_platform_wayland.cc",
    "ozone_platform_wayland.h",
  ]

  import("//ui/base/ui_features.gni")
  if (use_xkbcommon) {
    configs += [ "//ui/events/ozone/layout:xkbcommon" ]
  }

  deps = [
    "//base",
    "//build:chromeos_buildflags",
    "//build/config/linux/libdrm",
    "//components/exo/wayland/protocol:aura_shell_protocol",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/system",
    "//skia",
    "//third_party/wayland-protocols:cursor_shapes_protocol",
    "//third_party/wayland-protocols:extended_drag",
    "//third_party/wayland-protocols:gtk_primary_selection_protocol",
    "//third_party/wayland-protocols:keyboard_extension_protocol",
    "//third_party/wayland-protocols:linux_dmabuf_protocol",
    "//third_party/wayland-protocols:linux_explicit_synchronization_protocol",
    "//third_party/wayland-protocols:presentation_time_protocol",
    "//third_party/wayland-protocols:primary_selection_protocol",
    "//third_party/wayland-protocols:text_input_protocol",
    "//third_party/wayland-protocols:viewporter_protocol",
    "//third_party/wayland-protocols:wayland_drm_protocol",
    "//third_party/wayland-protocols:xdg_decoration_protocol",
    "//third_party/wayland-protocols:xdg_foreign",
    "//third_party/wayland-protocols:xdg_shell_protocol",
    "//ui/base",
    "//ui/base:buildflags",
    "//ui/base/cursor",
    "//ui/base/cursor:cursor_base",
    "//ui/base/cursor:theme_manager",
    "//ui/base/cursor/mojom:cursor_type",
    "//ui/base/ime/linux",
    "//ui/events",
    "//ui/events:dom_keycode_converter",
    "//ui/events/keycodes:xkb",
    "//ui/events/ozone",
    "//ui/events/ozone/evdev",
    "//ui/events/ozone/layout",
    "//ui/events/platform",
    "//ui/gfx",
    "//ui/gfx:memory_buffer",
    "//ui/gfx/geometry",
    "//ui/gfx/linux:drm",
    "//ui/ozone:ozone_base",
    "//ui/ozone/common",
    "//ui/ozone/public/mojom/wayland:wayland_mojom",
    "//ui/platform_window",
    "//ui/platform_window/wm",
  ]

  if (is_linux || is_chromeos_lacros) {
    deps += [ "//ui/base/ime/linux" ]
  }

  if (use_gtk) {
    sources += [
      "host/gtk_ui_delegate_wayland.cc",
      "host/gtk_ui_delegate_wayland.h",
    ]
    deps += [
      "//build/config/linux/gtk",
      "//ui/gtk:gtk_ui_delegate",
    ]
  }

  if (is_chromeos_lacros) {
    deps += [
      "//chromeos/crosapi/cpp",
      "//chromeos/crosapi/mojom",
      "//chromeos/lacros",
    ]
  }

  defines = [ "OZONE_IMPLEMENTATION" ]

  if (use_system_libwayland) {
    defines += [ "USE_LIBWAYLAND_STUBS" ]
    deps += [ "//third_party/wayland:wayland_stubs" ]
  } else {
    deps += [
      "//third_party/wayland:wayland_client",
      "//third_party/wayland:wayland_cursor",
      "//third_party/wayland:wayland_egl",
    ]
  }

  if (use_wayland_gbm) {
    defines += [ "WAYLAND_GBM" ]
    sources += [
      "gpu/drm_render_node_handle.cc",
      "gpu/drm_render_node_handle.h",
      "gpu/gbm_pixmap_wayland.cc",
      "gpu/gbm_pixmap_wayland.h",
      "gpu/gbm_surfaceless_wayland.cc",
      "gpu/gbm_surfaceless_wayland.h",
    ]

    deps += [
      "//third_party/minigbm",
      "//ui/gfx:memory_buffer",
      "//ui/gfx/linux:gbm",
    ]
  }

  if (use_bundled_weston) {
    data_deps = [ "//third_party/weston" ]
  }

  if (enable_vulkan) {
    sources += [
      "gpu/vulkan_implementation_wayland.cc",
      "gpu/vulkan_implementation_wayland.h",
    ]
    deps += [ "//gpu/vulkan" ]
  }

  configs += [ "//third_party/khronos:khronos_headers" ]
}
