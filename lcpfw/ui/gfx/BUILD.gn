# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ozone.gni")
import("//build/config/ui.gni")
#import("//device/vr/buildflags/buildflags.gni")
import("//testing/libfuzzer/fuzzer_test.gni")
import("//testing/test.gni")

if (is_android) {
  import("//build/config/android/config.gni")
  import("//build/config/android/rules.gni")
}

# Several targets want to include this header file, and some of them are
# child dependencies of "gfx". Therefore, we separate it out here so multiple
# targets can all have a dependency for header checking purposes without
# creating circular dependencies.
source_set("gfx_export") {
  sources = [ "gfx_export.h" ]
}

# Used for color generation at build time without importing all the gfx.
component("color_utils") {
  sources = [
    "color_palette.h",
    "color_utils.cc",
    "color_utils.h",
  ]
  defines = [ "GFX_IMPLEMENTATION" ]
  public_deps = [
    ":gfx_export",
    "//base",
    "//skia",
    "//ui/gfx/geometry",
  ]
}

component("geometry_skia") {
  sources = [
    "geometry_skia_export.h",
    "mask_filter_info.cc",
    "mask_filter_info.h",
    "rrect_f.cc",
    "rrect_f.h",
    "rrect_f_builder.cc",
    "rrect_f_builder.h",
    "skia_util.cc",
    "skia_util.h",
    "transform.cc",
    "transform.h",
    "transform_operation.cc",
    "transform_operation.h",
    "transform_operations.cc",
    "transform_operations.h",
    "transform_util.cc",
    "transform_util.h",
  ]
  configs += [ "//build/config/compiler:wexit_time_destructors" ]
  deps = [
    "//base",
    "//skia",
    "//ui/gfx/geometry",
  ]
  defines = [ "GEOMETRY_SKIA_IMPLEMENTATION" ]
}

component("gfx") {
  sources = [
    "break_list.h",
    "color_analysis.cc",
    "color_analysis.h",
    "color_transform.cc",
    "color_transform.h",
    "decorated_text.cc",
    "decorated_text.h",
    "extension_set.cc",
    "extension_set.h",
    "favicon_size.cc",
    "favicon_size.h",
    "font.cc",
    "font.h",
    "font_fallback.h",
    "font_list.cc",
    "font_list.h",
    "font_list_impl.cc",
    "font_list_impl.h",
    "font_render_params.cc",
    "font_render_params.h",
    "font_util.cc",
    "font_util.h",
    "gdi_util.cc",
    "gdi_util.h",
    "gpu_extra_info.cc",
    "gpu_extra_info.h",
    "half_float.cc",
    "half_float.h",
    "icon_util.cc",
    "icon_util.h",
    "image/image.cc",
    "image/image.h",
    "image/image_family.cc",
    "image/image_family.h",
    "image/image_platform.h",
    "image/image_png_rep.cc",
    "image/image_png_rep.h",
    "image/image_skia.cc",
    "image/image_skia.h",
    "image/image_skia_rep.h",
    "image/image_skia_source.cc",
    "image/image_skia_source.h",
    "image/image_util.cc",
    "image/image_util.h",
    "interpolated_transform.cc",
    "interpolated_transform.h",
    "nine_image_painter.cc",
    "nine_image_painter.h",
    "overlay_transform_utils.cc",
    "overlay_transform_utils.h",
    "platform_font.h",
    "rendering_pipeline.cc",
    "rendering_pipeline.h",
    "rendering_stage_scheduler.cc",
    "rendering_stage_scheduler.h",
    "scrollbar_size.cc",
    "scrollbar_size.h",
    "selection_model.cc",
    "selection_model.h",
    "sequential_id_generator.cc",
    "sequential_id_generator.h",
    "shadow_value.cc",
    "shadow_value.h",
    "skbitmap_operations.cc",
    "skbitmap_operations.h",
    "swap_result.cc",
    "sys_color_change_listener.cc",
    "sys_color_change_listener.h",
    "text_constants.h",
    "text_elider.cc",
    "text_elider.h",
    "text_utils.cc",
    "text_utils.h",
    "ui_gfx_exports.cc",
    "utf16_indexing.cc",
    "utf16_indexing.h",
    "video_types.h",
    "vsync_provider.cc",
    "vsync_provider.h",
  ]
  if (is_android) {
    sources += [
      "android/android_surface_control_compat.cc",
      "android/android_surface_control_compat.h",
      "android/java_bitmap.cc",
      "android/java_bitmap.h",
      "android/view_configuration.cc",
      "android/view_configuration.h",
    ]
  }
  if (is_linux || is_chromeos) {
    sources += [
      "font_fallback_linux.cc",
      "font_fallback_linux.h",
      "font_render_params_linux.cc",
      "linux/fontconfig_util.cc",
      "linux/fontconfig_util.h",
    ]
  }
  if (is_mac) {
    sources += [
      "canvas_paint_mac.h",
      "canvas_paint_mac.mm",
      "decorated_text_mac.h",
      "decorated_text_mac.mm",
      "font_fallback_mac.mm",
      "font_render_params_mac.cc",
      "image/image_mac.mm",
      "image/image_skia_util_mac.h",
      "image/image_skia_util_mac.mm",
      "image/image_util_mac.mm",
      "mac/coordinate_conversion.h",
      "mac/coordinate_conversion.mm",
      "mac/nswindow_frame_controls.h",
      "mac/nswindow_frame_controls.mm",
      "mac/scoped_cocoa_disable_screen_updates.h",
      "mac/scoped_cocoa_disable_screen_updates.mm",
      "path_mac.h",
      "path_mac.mm",
      "platform_font_mac.h",
      "platform_font_mac.mm",
      "scoped_cg_context_save_gstate_mac.h",
      "scoped_ns_graphics_context_save_gstate_mac.h",
      "scoped_ns_graphics_context_save_gstate_mac.mm",
    ]
  }
  if (is_win) {
    sources += [
      "font_fallback_win.cc",
      "font_fallback_win.h",
      "font_render_params_win.cc",
      "path_win.cc",
      "path_win.h",
      "system_fonts_win.cc",
      "system_fonts_win.h",
      "win/crash_id_helper.cc",
      "win/crash_id_helper.h",
      "win/direct_write.cc",
      "win/direct_write.h",
      "win/hwnd_util.cc",
      "win/hwnd_util.h",
      "win/msg_util.h",
      "win/physical_size.cc",
      "win/physical_size.h",
      "win/rendering_window_manager.cc",
      "win/rendering_window_manager.h",
      "win/scoped_set_map_mode.h",
      "win/singleton_hwnd.cc",
      "win/singleton_hwnd.h",
      "win/singleton_hwnd_hot_key_observer.cc",
      "win/singleton_hwnd_hot_key_observer.h",
      "win/singleton_hwnd_observer.cc",
      "win/singleton_hwnd_observer.h",
      "win/text_analysis_source.cc",
      "win/text_analysis_source.h",
      "win/window_impl.cc",
      "win/window_impl.h",
    ]
  }
  if (is_ios) {
    sources += [
      "image/image_ios.mm",
      "image/image_skia_rep_ios.cc",
      "image/image_skia_rep_ios.h",
      "image/image_skia_util_ios.h",
      "image/image_skia_util_ios.mm",
      "image/image_util_ios.mm",
      "ios/NSString+CrStringDrawing.h",
      "ios/NSString+CrStringDrawing.mm",
      "ios/uikit_util.h",
      "ios/uikit_util.mm",
      "platform_font_ios.h",
      "platform_font_ios.mm",
      "scoped_ui_graphics_push_context_ios.h",
      "scoped_ui_graphics_push_context_ios.mm",
      "text_utils_ios.mm",
    ]
  }
  if (!is_ios) {
    sources += [
      "blit.cc",
      "blit.h",
      "canvas.cc",
      "canvas.h",
      "canvas_skia.cc",
      "canvas_skia_paint.h",
      "image/canvas_image_source.cc",
      "image/canvas_image_source.h",
      "image/image_generic.cc",
      "image/image_skia_operations.cc",
      "image/image_skia_operations.h",
      "image/image_skia_rep_default.cc",
      "image/image_skia_rep_default.h",
      "paint_throbber.cc",
      "paint_throbber.h",
      "scoped_canvas.cc",
      "scoped_canvas.h",
      "shadow_util.cc",
      "shadow_util.h",
      "skia_paint_util.cc",
      "skia_paint_util.h",
      "skia_vector_animation.cc",
      "skia_vector_animation.h",
      "skia_vector_animation_observer.h",
    ]
  }

  configs += [
    "//build/config:precompiled_headers",
    "//build/config/compiler:noshadowing",
    "//build/config/compiler:wexit_time_destructors",
  ]

  # This is part of the gfx component in the component build.
  defines = [ "GFX_IMPLEMENTATION" ]

  public_deps = [
    ":color_space",
    ":color_utils",
    ":geometry_skia",
    ":gfx_switches",
    ":memory_buffer_sources",
    ":native_widget_types",
    ":resize_image_dimensions",
    ":selection_bound_sources",
    "//base",
    "//skia",
    "//skia:skcms",
    "//third_party/icu",
    "//ui/gfx/animation",
    "//ui/gfx/codec",
    "//ui/gfx/geometry",
    "//ui/gfx/range",
  ]
  deps = [
    ":gfx_export",
    "//base",
    "//base:base_static",
    "//base:i18n",
    "//base/third_party/dynamic_annotations",
    "//build:chromeos_buildflags",
    #"//device/vr/buildflags",
    "//skia",
    "//third_party/zlib",
  ]

  if (!is_apple) {
    sources += [
      "platform_font_skia.cc",
      "platform_font_skia.h",
      "skia_font_delegate.cc",
      "skia_font_delegate.h",
    ]
  }

  # iOS.
  if (is_ios) {
    sources += [ "scoped_cg_context_save_gstate_mac.h" ]
  } else {
    public_deps += [ "//cc/paint" ]
    deps += [ "//third_party:freetype_harfbuzz" ]
  }

  # Android.
  if (is_android) {
    if (!is_debug) {
      configs -= [ "//build/config/compiler:default_optimization" ]
      configs += [ "//build/config/compiler:optimize_max" ]
    }

    deps += [ ":gfx_jni_headers" ]
    libs = [
      "android",
      "jnigraphics",
    ]
  }

  if (is_android || is_fuchsia) {
    sources += [
      "font_fallback_skia.cc",
      "font_render_params_skia.cc",
    ]
  }

  if (is_android || is_fuchsia || is_win || is_mac) {
    sources += [
      "font_fallback_skia_impl.cc",
      "font_fallback_skia_impl.h",
    ]
  }

  if (!is_ios) {
    sources += [
      "bidi_line_iterator.cc",
      "bidi_line_iterator.h",
      "harfbuzz_font_skia.cc",
      "harfbuzz_font_skia.h",
      "paint_vector_icon.cc",
      "paint_vector_icon.h",
      "render_text.cc",
      "render_text.h",
      "render_text_harfbuzz.cc",
      "render_text_harfbuzz.h",
      "text_utils_skia.cc",
      "vector_icon_types.h",
    ]
  }

  # Windows.
  if (is_win) {
    libs = [
      "setupapi.lib",
      "dwrite.lib",
    ]
    deps += [ "//components/crash/core/common" ]
  } else {
    sources -= [
      "gdi_util.cc",
      "gdi_util.h",
      "icon_util.cc",
      "icon_util.h",
      "sys_color_change_listener.cc",
      "sys_color_change_listener.h",
    ]
  }

  # Linux.
  if (is_linux || is_chromeos) {
    deps += [ "//third_party/fontconfig" ]
  }

  if (is_mac) {
    frameworks = [
      "AppKit.framework",
      "CoreFoundation.framework",
      "CoreGraphics.framework",
      "CoreText.framework",
      "IOSurface.framework",
    ]
  }

  if ((!use_aura && !toolkit_views) || is_ios) {
    sources -= [
      "nine_image_painter.cc",
      "nine_image_painter.h",
    ]
  }

  if (use_ozone) {
    deps += [ "//ui/ozone:buildflags" ]
  }

  if (ozone_platform_x11 || use_x11) {
    deps += [ "//ui/gfx/x" ]
  }
}

component("color_space") {
  sources = [
    "color_space.cc",
    "color_space.h",
    "color_space_export.h",
    "display_color_spaces.cc",
    "display_color_spaces.h",
    "icc_profile.cc",
    "icc_profile.h",
    "skia_color_space_util.cc",
    "skia_color_space_util.h",
  ]
  if (is_win) {
    sources += [
      "color_space_win.cc",
      "color_space_win.h",
    ]
  }
  deps = [
    "//build:chromeos_buildflags",
    "//skia:skcms",
    "//ui/gfx:buffer_types",
  ]
  public_deps = [
    "//base",
    "//skia",
  ]
  if (is_mac) {
    sources += [
      "mac/display_icc_profiles.cc",
      "mac/display_icc_profiles.h",
    ]
    frameworks = [
      "CoreFoundation.framework",
      "CoreGraphics.framework",
    ]
  }
  if (use_x11) {
    deps += [ "//ui/gfx/x" ]
  }
  defines = [ "COLOR_SPACE_IMPLEMENTATION" ]
}

# Depend on this to use image/resize_image_dimensions.h without pulling in
# all of gfx.
source_set("resize_image_dimensions") {
  sources = [ "image/resize_image_dimensions.h" ]
}

# Depend on this to use native_widget_types.h without pulling in all of gfx.
source_set("native_widget_types") {
  public = [ "native_widget_types.h" ]

  public_deps = [
    ":gfx_export",
    "//base",
  ]

  deps = [ "//build:chromeos_buildflags" ]
}

group("selection_bound") {
  if (is_component_build) {
    public_deps = [ ":gfx" ]
  } else {
    public_deps = [ ":selection_bound_sources" ]
  }
}

# Depend on this to use selection_bound.h without pulling in all of gfx.
# Cannot be a static_library in component builds due to exported functions
source_set("selection_bound_sources") {
  visibility = [ ":*" ]

  sources = [
    "gfx_export.h",
    "selection_bound.cc",
    "selection_bound.h",
  ]

  configs += [ "//build/config/compiler:wexit_time_destructors" ]

  defines = [ "GFX_IMPLEMENTATION" ]

  public_deps = [
    "//base",
    "//ui/gfx/geometry",
  ]
}

# Depend on this to use buffer_types.h without pulling in all of gfx.
source_set("buffer_types") {
  sources = [ "buffer_types.h" ]
}

# The GPU memory buffer stuff is separate from "gfx" to allow GPU-related
# things to use these files without pulling in all of gfx, which includes large
# things like Skia.
#
# The structure here allows the memory buffer to be part of the gfx component
# in the component build, but be a separate source set in a static build.
group("memory_buffer") {
  if (is_component_build) {
    public_deps = [ ":gfx" ]
  } else {
    public_deps = [ ":memory_buffer_sources" ]
  }
}

# Cannot be a static_library in component builds due to exported functions
source_set("memory_buffer_sources") {
  visibility = [ ":*" ]  # Depend on through ":memory_buffer".

  # TODO(brettw) refactor this so these sources are in a coherent directory
  # structure rather than random samplings of ui/gfx and ui/gfx/mac.
  sources = [
    "buffer_format_util.cc",
    "buffer_format_util.h",
    "buffer_usage_util.cc",
    "buffer_usage_util.h",
    "ca_layer_params.cc",
    "ca_layer_params.h",
    "client_native_pixmap.h",
    "client_native_pixmap_factory.h",
    "generic_shared_memory_id.cc",
    "generic_shared_memory_id.h",
    "gfx_export.h",
    "gpu_fence.cc",
    "gpu_fence.h",
    "gpu_fence_handle.cc",
    "gpu_fence_handle.h",
    "hdr_metadata.cc",
    "hdr_metadata.h",
    "native_pixmap.h",
    "overlay_transform.h",
    "surface_origin.h",
  ]

  if (!is_ios) {
    sources += [
      "gpu_memory_buffer.cc",
      "gpu_memory_buffer.h",
    ]
  }

  configs += [ "//build/config/compiler:wexit_time_destructors" ]

  defines = [ "GFX_IMPLEMENTATION" ]

  public_deps = [ ":buffer_types" ]

  deps = [
    ":gfx_switches",
    ":native_widget_types",
    "//base",
    "//build:chromecast_buildflags",
    "//build:chromeos_buildflags",
    "//ui/gfx/geometry",
  ]

  if (is_linux || is_chromeos) {
    sources += [
      "linux/client_native_pixmap_dmabuf.cc",
      "linux/client_native_pixmap_dmabuf.h",
      "linux/client_native_pixmap_factory_dmabuf.cc",
      "linux/client_native_pixmap_factory_dmabuf.h",
      "linux/native_pixmap_dmabuf.cc",
      "linux/native_pixmap_dmabuf.h",
    ]

    deps += [ "//build/config/linux/libdrm" ]
  }

  if (is_linux || is_chromeos || is_android) {
    deps += [ "//third_party/libsync" ]
  }

  if (is_mac) {
    sources += [
      "mac/io_surface.cc",
      "mac/io_surface.h",
    ]

    public_deps += [ "//ui/gfx:color_space" ]
  }

  if (is_win) {
    public_deps += [ "//ipc:message_support" ]
  }

  if ((is_linux || is_chromeos || use_ozone) && !is_nacl) {
    sources += [
      "native_pixmap_handle.cc",
      "native_pixmap_handle.h",
    ]
  }
}

# TODO(ccameron): This can be moved into a separate source_set.
component("gfx_switches") {
  sources = [
    "switches.cc",
    "switches.h",
    "switches_export.h",
  ]

  defines = [ "GFX_SWITCHES_IMPLEMENTATION" ]

  deps = [ "//base" ]
}

if (is_mac) {
  component("gfx_io_surface_hdr_metadata") {
    sources = [
      "mac/io_surface_hdr_metadata.cc",
      "mac/io_surface_hdr_metadata.h",
    ]
    defines = [ "IS_GFX_IO_SURFACE_HDR_METADATA_IMPL" ]

    # This is a separate component from the other sources because it depends on
    # the mojo serialize and deserialize methods.
    deps = [
      ":gfx",
      "//ui/gfx/mojom:mojom",
    ]
    frameworks = [
      "CoreFoundation.framework",
      "IOSurface.framework",
    ]
  }
}

if (is_android) {
  generate_jni("gfx_jni_headers") {
    sources = [
      "../android/java/src/org/chromium/ui/gfx/Animation.java",
      "../android/java/src/org/chromium/ui/gfx/BitmapHelper.java",
      "../android/java/src/org/chromium/ui/gfx/ViewConfigurationHelper.java",
    ]
  }
}
